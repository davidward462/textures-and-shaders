<!DOCTYPE html>
<html>

<script id="vertex-shader" type="x-shader/x-vertex">#version 300 es

    // Vertex Shader

    in vec4 vPosition;
    in vec3 vNormal;
    in vec2 vTexCoord;
    
    uniform mat4 modelViewMatrix;
    uniform mat4 normalMatrix;
    uniform mat4 projectionMatrix;
    
    out vec3 fPos;
    out vec3 fNormal;
    out vec2 fTexCoord;
    
    void main() {
        
        // Transform vertex position to eye coordinates
        fPos = vec3(modelViewMatrix * vPosition);
        
        // Transform vertex normal into eye coordinates
        fNormal = normalize(vec3(normalMatrix * vec4(vNormal, 0.0)));
        
        // Pass through the texture coordinates
        fTexCoord = vTexCoord;
        
        // Final vertex position
        gl_Position = projectionMatrix * modelViewMatrix * vPosition;
    }

</script>

<script id="fragment-shader" type="x-shader/x-fragment">#version 300 es

    precision mediump float;

    in vec3 fPos;
    in vec3 fNormal;
    in vec2 fTexCoord;

    // texture variables
    uniform sampler2D texture0;
    uniform sampler2D texture1;
    uniform sampler2D texture2;
    uniform int useTextures;
    uniform int textureIndex;

    uniform vec4 ambientProduct, diffuseProduct, specularProduct;
    uniform vec4 lightPosition;
    uniform float shininess;

    layout(location=0) out vec4 fragColor;

    void main() {

        // Normalize interpolated normal
        vec3 N = normalize(fNormal);

        // Vector from fragment position to light source
        vec3 L = normalize(lightPosition.xyz - fPos);
        
        vec3 V = normalize(-fPos);
        
        // Halfway vector for Blinn-Phong 
        vec3 H = normalize(L + V); 

        // diffuse component
        float lightDotNormal = max(dot(L, N), 0.0);
        vec4 diffuse = diffuseProduct * lightDotNormal;

        // Compute Blinn-Phong specular component
        float specularCoefficient = pow(max(dot(N, H), 0.0), shininess);
        vec4 specular = vec4(0.0, 0.0, 0.0, 0.0); // Initialize specular to black

        // Check if the surface is lit by the light source
        if(lightDotNormal > 0.0) 
        {
            specular = specularProduct * specularCoefficient;
        }
        
        // Combine the lighting components
        vec4 surfaceColor = ambientProduct + diffuse + specular;

        // Also apply lighting to textured surfaces
        if(useTextures == 1) 
        {
            if (textureIndex == 0) 
            {
                surfaceColor *= texture(texture0, fTexCoord);
            } 
            else if (textureIndex == 1) 
            {
                surfaceColor *= texture(texture1, fTexCoord);
            }
        }
        
        // Output the final color
        fragColor = surfaceColor;
    }

</script>

<script type="text/javascript" src="Common/webgl-utils.js"></script>
<script type="text/javascript" src="Common/initShaders.js"></script>

<script type="text/javascript" src="Common/MV.js"></script>
<script type="text/javascript" src="objects.js"></script>
<script type="text/javascript" src="main.js"></script>


<body style="background-color:#000000;">
<canvas id="gl-canvas" width="512" height="512">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>

<br/>
<br/>

<div id="animToggleButton">
    <input id="animToggleButton" type="button" value="Toggle Animation"
    />

<div id="viewToggleButton">
    <input id="viewToggleButton" type="button" value="Toggle View"/>

</div><br/>

</div><br/>



</body>
</html>
