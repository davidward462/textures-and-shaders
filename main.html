<!DOCTYPE html>
<html>

<script id="vertex-shader" type="x-shader/x-vertex">#version 300 es

    // Vertex Shader

    in vec4 vPosition;
    in vec3 vNormal;
    in vec2 vTexCoord;
    
    uniform mat4 modelViewMatrix;
    uniform mat4 normalMatrix;
    uniform mat4 projectionMatrix;
    
    out vec3 fPos;
    out vec3 fNormal;
    out vec2 fTexCoord;
    
    void main() {
        
        // Transform vertex position to eye coordinates
        fPos = vec3(modelViewMatrix * vPosition);
        
        // Transform vertex normal into eye coordinates
        fNormal = normalize(vec3(normalMatrix * vec4(vNormal, 0.0)));
        
        // Pass through the texture coordinates
        fTexCoord = vTexCoord;
        
        // Final vertex position
        gl_Position = projectionMatrix * modelViewMatrix * vPosition;
    }

</script>

<script id="fragment-shader" type="x-shader/x-fragment">#version 300 es

// Fragment Shader

precision mediump float;

in vec3 fPos;
in vec3 fNormal;
in vec2 fTexCoord;

// texture variables
uniform sampler2D texture0;
uniform sampler2D texture1;
uniform int useTextures;
uniform int textureIndex;

uniform vec4 ambientProduct, diffuseProduct, specularProduct;
uniform vec4 lightPosition;
uniform float shininess;

layout(location=0) out vec4 fragColor;

void main() {

    // Normalize interpolated normal
    vec3 N = normalize(fNormal);

    // Vector from fragment position to light
    vec3 L = normalize(lightPosition.xyz - fPos);
    
    // Vector from fragment position to eye (assuming the eye is at the origin)
    vec3 V = normalize(-fPos);
    
    // Reflected light vector
    vec3 R = reflect(-L, N);

    // Compute diffuse component
    float lightDotNormal = max( dot(L, N), 0.0);
    vec4 diffuse = vec4(0.0, 0.0, 0.0, 1.0);
    diffuse = diffuseProduct * lightDotNormal;

    // Compute specular component
    vec4 specular = specularProduct * pow( max(dot(R, V), 0.0), shininess);

    // If the surface is facing away from the light source, there's no specular component
    if( dot(L, N) < 0.0 ) {
        specular = vec4(0.0, 0.0, 0.0, 1.0);
    }

    // summation of components to fragment color
    fragColor = ambientProduct + diffuse + specular;

    // TODO: make lighting happen on textures
    if( useTextures == 1)
    {
        if (textureIndex == 0)
        {
            fragColor = texture( texture0, fTexCoord );
        }
        
        if (textureIndex == 1)
        {
            fragColor = texture( texture1, fTexCoord );
        }
    }
}

</script>

<script type="text/javascript" src="Common/webgl-utils.js"></script>
<script type="text/javascript" src="Common/initShaders.js"></script>

<script type="text/javascript" src="Common/MV.js"></script>
<script type="text/javascript" src="objects.js"></script>
<script type="text/javascript" src="main.js"></script>


<body style="background-color:#000000;">
<canvas id="gl-canvas" width="512" height="512">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>

<br/>
<br/>

<div id="animToggleButton">
    <input id="animToggleButton" type="button" value="Toggle Animation"
    />

</div><br/>

</div><br/>



</body>
</html>
